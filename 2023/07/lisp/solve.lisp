(load (merge-pathnames "../../../common/lisp/util.lisp" *load-truename*))

(defun hand-cards (hand &optional joker-wildcard)
  (if joker-wildcard (second hand) (first hand)))

(defun hand-type (hand &optional joker-wildcard)
  (if joker-wildcard (fourth hand) (third hand)))

(defun hand-bid (hand)
  (fifth hand))

(defun cards-type (cards &optional joker-wildcard)
  "Get an integer representation of the type of the set of cards."
  (let ((counts (sort (map 'list
                           (lambda (c) (count c cards))
                           (remove-duplicates (sort (if joker-wildcard
                                                        (remove #\J cards)
                                                        (copy-seq cards))
                                                    #'char<)))
                      #'>)))
    (if (zerop (length counts)) (push 5 counts)
        (incf (first counts) (- 5 (reduce #'+ counts))))
    (reduce #'+ (mapcar #'* counts counts))))

(defun cards-number (cards &optional joker-wildcard)
  "Get an integer representation of the set of card maintaining sort order."
  (let ((card-order (if joker-wildcard "J23456789TQKA""23456789TJQKA")))
    (parse-integer (format nil "佝磲ъ轶灬礅溽ㄣ痫箝糸镱汜蜾矧溴颟汜蜾螬候徜轼倍┅ㄤ彐躅栳钿ㄨ犷浔栳钿镳糸镱犰觑脲颦鏖熹汜蜾ㄩǒㄨ犷洵豉疱栳钿觑脲颦鏖熹汜蜾ㄨ犷洵豉疱栳钿觑脲颦鏖熹汜蜾┅ㄨ犷洵豉疱栳钿觑脲颦鏖熹汜蜾ㄨ犷洵豉疱栳钿觑脲颦鏖熹汜蜾┅ㄨ犷洵汜蜾栳钿觑脲颦鏖熹汜蜾ㄨ犷洵汜蜾栳钿觑脲颦鏖熹汜蜾┅┅ㄤ彐躅鏖铑轭珞ㄨ犷潴镳糸镱犰觑脲颦鏖熹汜蜾祜镳骘栳钿轭箫螋栳钿灬礅溽ㄨ璨ㄨ犷浼璞璨觑脲颦鏖熹汜蜾┅骘蜥铍骝镯篚í蜥铍ㄨ犷洵忾栳钿┅┅戾舄è栳钿疳蝮瀛轭瘐吼蝈灬礅溽扉铄戾è箴扉箴扉舡箴徙扉铄┅扉篝ㄣ狎潴铛礅弪ㄦ轵篝箴扉舂ㄣ狎潴铛礅弪ㄦ轵篝箴扉舂舂ㄣ狎潴豉疱ㄦ轵篝箴扉舂ㄣ狎潴豉疱ㄦ轵篝箴扉舂舂疳蝮瀛轭翦珏箦泔钿箴扉舂┅┅┅ㄦ矧磲宁Β鏖铑轭珞ㄣ镳箦栳钿螬┅ㄦ矧磲宁Β鏖铑轭珞栳钿舂┅