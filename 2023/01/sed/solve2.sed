#!/usr/bin/env -S sed -Ef

# get first number
s/one/o1e/g
s/two/t2o/g
s/three/t3e/g
s/four/4/g
s/five/5e/g
s/six/6/g
s/seven/7n/g
s/eight/e8t/g
s/nine/n9e/g
s/^[a-z]*([0-9]).*([0-9])[a-z]*$/\1\2/
s/^[a-z]*([0-9])[a-z]*$/\1\1/

:loop
# join next line
N
s/\n/ /
/^[0-9]$/bend
# get next number
s/one/o1e/g
s/two/t2o/g
s/three/t3e/g
s/four/4/g
s/five/5e/g
s/six/6/g
s/seven/7n/g
s/eight/e8t/g
s/nine/n9e/g
s/ [a-z]*([0-9]).*([0-9])[a-z]*$/ \1\2/
s/ [a-z]*([0-9])[a-z]*$/ \1\1/
# add both numbers together
:add
# preprocessing: takes two space-seperated numbers and initializes carry and result
s/^([0-9]*) ([0-9]*)$/z\1 \2 0 /

# start single addition step: move least significant digit of both summands and
#                             carry to front
s/^z([0-9]*)([0-9]) ([0-9]*)([0-9]) ([0,1]) ([0-9]*)$/a\2\4\5 \1 \3 \6/

# add to digits and get new carry
s/^a(00)([0,1] [0-9]* [0-9]*) ([0-9]*)$/c0\2 0 \3/
s/^a(01|10)([0,1] [0-9]* [0-9]*) ([0-9]*)$/c1\2 0 \3/
s/^a(02|20|11)([0,1] [0-9]* [0-9]*) ([0-9]*)$/c2\2 0 \3/
s/^a(03|30|12|21)([0,1] [0-9]* [0-9]*) ([0-9]*)$/c3\2 0 \3/
s/^a(04|40|13|31|22)([0,1] [0-9]* [0-9]*) ([0-9]*)$/c4\2 0 \3/
s/^a(05|50|14|41|23|32)([0,1] [0-9]* [0-9]*) ([0-9]*)$/c5\2 0 \3/
s/^a(06|60|15|51|24|42|33)([0,1] [0-9]* [0-9]*) ([0-9]*)$/c6\2 0 \3/
s/^a(07|70|16|61|25|52|34|43)([0,1] [0-9]* [0-9]*) ([0-9]*)$/c7\2 0 \3/
s/^a(08|80|17|71|26|62|35|53|44)([0,1] [0-9]* [0-9]*) ([0-9]*)$/c8\2 0 \3/
s/^a(09|90|18|81|27|72|36|63|45|54)([0,1] [0-9]* [0-9]*) ([0-9]*)$/c9\2 0 \3/
s/^a(19|91|28|82|37|73|46|64|55)([0,1] [0-9]* [0-9]*) ([0-9]*)$/c0\2 1 \3/
s/^a(29|92|38|83|47|74|56|65)([0,1] [0-9]* [0-9]*) ([0-9]*)$/c1\2 1 \3/
s/^a(39|93|48|84|57|75|66)([0,1] [0-9]* [0-9]*) ([0-9]*)$/c2\2 1 \3/
s/^a(49|94|58|85|67|76)([0,1] [0-9]* [0-9]*) ([0-9]*)$/c3\2 1 \3/
s/^a(59|95|68|86|77)([0,1] [0-9]* [0-9]*) ([0-9]*)$/c4\2 1 \3/
s/^a(69|96|78|87)([0,1] [0-9]* [0-9]*) ([0-9]*)$/c5\2 1 \3/
s/^a(79|97|88)([0,1] [0-9]* [0-9]*) ([0-9]*)$/c6\2 1 \3/
s/^a(89|98)([0,1] [0-9]* [0-9]*) ([0-9]*)$/c7\2 1 \3/
s/^a(99)([0,1] [0-9]* [0-9]*) ([0-9]*)$/c8\2 1 \3/
# add carry and potentially set new carry
s/^c(00) ([0-9]* [0-9]*) ([0,1]) ([0-9]*)$/z\2 \3 0\4/
s/^c(10|01) ([0-9]* [0-9]*) ([0,1]) ([0-9]*)$/z\2 \3 1\4/
s/^c(20|11) ([0-9]* [0-9]*) ([0,1]) ([0-9]*)$/z\2 \3 2\4/
s/^c(30|21) ([0-9]* [0-9]*) ([0,1]) ([0-9]*)$/z\2 \3 3\4/
s/^c(40|31) ([0-9]* [0-9]*) ([0,1]) ([0-9]*)$/z\2 \3 4\4/
s/^c(50|41) ([0-9]* [0-9]*) ([0,1]) ([0-9]*)$/z\2 \3 5\4/
s/^c(60|51) ([0-9]* [0-9]*) ([0,1]) ([0-9]*)$/z\2 \3 6\4/
s/^c(70|61) ([0-9]* [0-9]*) ([0,1]) ([0-9]*)$/z\2 \3 7\4/
s/^c(80|71) ([0-9]* [0-9]*) ([0,1]) ([0-9]*)$/z\2 \3 8\4/
s/^c(90|81) ([0-9]* [0-9]*) ([0,1]) ([0-9]*)$/z\2 \3 9\4/
s/^c(91) ([0-9]* [0-9]*) ([0,1]) ([0-9]*)$/z\2 1 0\4/

# if only one summand is left but carry is still set simply set up another addition
s/^z([0-9]*)([0-9])  1 ([0-9]*)$/a\210 \1  \3/
s/^z ([0-9]*)([0-9]) 1 ([0-9]*)$/a\210  \1 \3/

# postprocessing: if only one carry left, simply prepend it
s/^z([0-9]+)  0 ([0-9]*)$/\1\2/
s/^z ([0-9]+) 0 ([0-9]*)$/\1\2/
# postprocessing: if no summand is left, prepend carry if set
s/^z  0 ([0-9]*)$/\1/
s/^z  1 ([0-9]*)$/1\1/

/^[0-9]*$/baddend
badd
:addend
bloop
:end
